buildscript {
    ext {
        springBootVersion = '1.3.0.M2'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-6'
    }
}

apply plugin: 'spring-boot'
apply plugin: 'java'
apply plugin: 'war'

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'

apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply from: 'gradle/integration-test.gradle'
apply plugin: 'com.github.ben-manes.versions'

findbugs.toolVersion = '3.0.0'

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

configurations {
    // exclude commons-logging, log4j, and j.u.logging - we use slf4j and logback
    //all*.exclude group: 'commons-logging'
    //all*.exclude group: 'log4j'
    all*.exclude group: 'jboss', module: 'javassist'
    providedRuntime
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.security:spring-security-taglibs:4.0.1.RELEASE'
    runtime 'mysql:mysql-connector-java'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    compile 'org.apache.tiles:tiles-extras:3.0.4'
    compile 'org.apache.derby:derby:10.11.1.1'
    compile 'org.apache.pdfbox:pdfbox:1.8.6'
    compile 'org.bouncycastle:bcprov-jdk16:1.46'
    compile 'org.bouncycastle:bcmail-jdk16:1.46'

    // jsp dependencies
    compile 'org.apache.tomcat.embed:tomcat-embed-jasper'
    compile 'javax.servlet:jstl'

    // misc dependencies
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'javax.mail:javax.mail-api:1.5.2'
    compile 'jstl:jstl:1.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.5.2'
    compile 'findbugs:annotations:1.0.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.sun.mail:javax.mail:1.5.2'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.7.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

war {
    baseName = 'scriptinat0r7'
    version = '0.0.1-SNAPSHOT'
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
        file {
            // Classpath entry for Eclipse which changes the order of classpathentries; otherwise no sources for 3rd party jars are shown
            withXml { xml ->
                def node = xml.asNode()
                node.remove( node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container' } )
                node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
            }
        }
    }
    jdt {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    wtp {
        facet {
            facet name: 'java', version: '1.8'
            facet name: 'jst.web', version: '3.0'
        }
    }
}

/* Changes the source encoding to UTF-8 */
eclipseJdt << {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=UTF-8')
}

/* Configures the PMD-Plugin globally. */
pmd {
    toolVersion  = '5.1.3'
}

/* Configures the Checkstyle-Plugin globally. */
checkstyle {
    toolVersion = '6.1'
}

/* Configures the PMD-Plugin for main and test */
tasks.withType(Pmd) {
    ruleSets = []
    ruleSetFiles = files('config/pmd/rules-hafner.xml')
    ignoreFailures = true
    reports {
        xml.enabled true
        html.enabled false
    }
}

tasks.withType(Checkstyle) {
    ignoreFailures = true
    config
    configProperties = ['config_loc':"${projectDir}/config/checkstyle"]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled true
        html.enabled false
    }
    ignoreFailures = true
    excludeFilter = file('config/findbugs/findbugs-exclusion-filter.xml')
}

// See http://www.practicalgradle.org/blog/2011/06/incremental-tests-with-jenkins/
task supressNoTestsRun {
    inputs.files test.outputs.files
    doLast{
        def timestamp = System.currentTimeMillis()
        testResultsDir.eachFile { it.lastModified = timestamp }
    }
}

build.dependsOn(supressNoTestsRun)

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
